apply plugin: 'java'
apply plugin: 'eclipse'

apply plugin: 'maven'

group = 'net.satago'

version = '1.0.3'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'

    def tapestry_version = '5.3.8'

    compile "org.apache.tapestry:tapestry-jpa:${tapestry_version}"
    compile 'javax.enterprise:cdi-api:1.2'

    testCompile 'org.testng:testng:6.9.10'
    testRuntime "org.apache.tapestry:tapestry-test:${tapestry_version}"

    testRuntime 'org.eclipse.persistence:eclipselink:2.5.1'

    def hibernate_version='5.1.0.Final'

    testCompile "org.hibernate:hibernate-entitymanager:${hibernate_version}"
    //  Version of javassist used by Hibernate 5
    testRuntime 'org.javassist:javassist:3.20.0-GA'

    testRuntime 'com.h2database:h2:1.4.191'
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'net.satago.tapestry5.jpa.TransactionalUnitsModule'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

jar.dependsOn sourcesJar
jar.dependsOn javadocJar

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: nexusRepositoryUrl) {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: nexusSnapshotRepositoryUrl) {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name 'tapestry-jpa-transactions'
                packaging 'jar'
                description 'Advanced transaction support for tapestry JPA applications'
                url 'https://github.com/satago/tapestry-jpa-transactions'

                issueManagement {
                    url 'https://github.com/satago/tapestry-jpa-transactions/issues'
                    system 'GitHub Issues'
                }

                scm {
                    url 'https://github.com/satago/tapestry-jpa-transactions.git'
                    connection 'scm:git://github.com/satago/tapestry-jpa-transactions.git'
                    developerConnection 'scm:git://github.com/satago/tapestry-jpa-transactions.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id "dmitrygusev"
                        name "Dmitry Gusev"
                        email "dmitry@anjlab.com"
                    }
                }
            }
        }
    }
}
